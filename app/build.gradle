apply plugin: 'com.android.application'

android {
    compileSdkVersion COMPILE_SDK_VERSION as int
    buildToolsVersion BUILD_TOOLS_VERSION

    defaultConfig {
        applicationId "com.roy.devil"
        minSdkVersion MIN_SDK_VERSION as int
        targetSdkVersion TARGET_SDK_VERSION as int
        versionCode 1
        versionName "1.0"
    }

    signingConfigs {
        release {
            storeFile file('../sign/jacob.keystore')
            storePassword '123456'
            keyAlias 'max'
            keyPassword '123456'
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            zipAlignEnabled true
            shrinkResources true
            signingConfig signingConfigs.release
        }
        debug {
            minifyEnabled false
            zipAlignEnabled false
            shrinkResources false
            signingConfig signingConfigs.debug
            applicationIdSuffix '.debug'
            versionNameSuffix "-debug"
        }
    }

    flavorDimensions "abi", "version"
    productFlavors {
        free {
            dimension 'version'
        }
        paid {
            dimension 'version'
        }
        x86 {
            dimension 'abi'
        }
        arm {
            dimension 'abi'
        }
    }

    // 批量修改apk名称
    android.applicationVariants.all { variant ->
        variant.outputs.all {
            if ('release' == variant.buildType.name) {
                outputFileName = "WaterlooBridge_${variant.productFlavors[0].name}_${variant.productFlavors[1].name}" +
                        "_v${variant.versionName}_${buildTime()}.apk"
            }
        }
    }

    lintOptions {
        disable 'Typos'
        abortOnError false
        absolutePaths false
        lintConfig file('lint.xml')
    }

    compileOptions {
        targetCompatibility JavaVersion.VERSION_1_8
        sourceCompatibility JavaVersion.VERSION_1_8
    }
}

static def buildTime() {
    def date = new Date()
    def formattedDate = date.format('yyyyMMdd')
    return formattedDate
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar', '*.aar'])
    implementation group: 'androidx.appcompat', name: 'appcompat', version: appcompat_version
    implementation group: 'androidx.cardview', name: 'cardview', version: '1.0.0'
    implementation group: 'androidx.constraintlayout', name: 'constraintlayout', version: constraintlayout_version

    implementation group: 'com.google.android.material', name: 'material', version: material_version
    implementation group: 'com.google.android', name: 'flexbox', version: flexbox_version

    implementation group: 'com.jakewharton', name: 'butterknife', version: butterknife_version
    annotationProcessor group: 'com.jakewharton', name: 'butterknife-compiler', version: butterknife_version

    implementation group: 'com.github.castorflex.verticalviewpager', name: 'library', version: '19.0.1'

//    debugImplementation group: 'com.squareup.leakcanary', name: 'leakcanary-android', version: leakcanary_version

    implementation group: 'com.github.bumptech.glide', name: 'glide', version: glide_version
    annotationProcessor group: 'com.github.bumptech.glide', name: 'compiler', version: glide_version
}
